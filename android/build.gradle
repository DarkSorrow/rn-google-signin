def safeExtGet(prop, fallback) {
  rootProject.ext.has(prop) ? rootProject.ext.get(prop) : fallback
}

def getExtOrIntegerDefault(name) {
  return rootProject.ext.has(name) ? rootProject.ext.get(name) : 21
}

def isNewArchitectureEnabled() {
  return rootProject.hasProperty("newArchEnabled") && rootProject.getProperty("newArchEnabled") == "true"
}

if (isNewArchitectureEnabled()) {
  apply plugin: "com.facebook.react"
}

apply plugin: 'com.android.library'
apply plugin: 'org.jetbrains.kotlin.android'

android {
  def agpVersion = com.android.Version.ANDROID_GRADLE_PLUGIN_VERSION
  if (agpVersion.tokenize('.')[0].toInteger() >= 7) {
    namespace "com.novastera.rngooglesignin"
  }
  
  compileSdkVersion getExtOrIntegerDefault('compileSdkVersion')

  defaultConfig {
    minSdkVersion getExtOrIntegerDefault('minSdkVersion')
    targetSdkVersion getExtOrIntegerDefault('targetSdkVersion')
    buildConfigField "boolean", "IS_NEW_ARCHITECTURE_ENABLED", isNewArchitectureEnabled().toString()
  }

  buildTypes {
    release {
      minifyEnabled false
    }
  }

  lintOptions {
    disable "GradleCompatible"
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }

  kotlinOptions {
    jvmTarget = "1.8"
  }

  sourceSets {
    main {
      if (isNewArchitectureEnabled()) {
        java.srcDirs += [
          "src/newarch",
          // This is needed to build Kotlin classes before Java classes
          "src/main/java"
        ]
      } else {
        java.srcDirs += [
          "src/oldarch",
          // This is needed to build Kotlin classes before Java classes
          "src/main/java"
        ]
      }
    }
  }
}

repositories {
  mavenCentral()
  google()
}



dependencies {
  //noinspection GradleDynamicVersion
  implementation "com.facebook.react:react-native:+"  // From node_modules
  implementation "com.google.android.gms:play-services-auth:${safeExtGet('googlePlayServicesAuthVersion', '21.3.0')}"
  implementation "androidx.credentials:credentials:${safeExtGet('credentialsVersion', '1.2.2')}"
  implementation "androidx.credentials:credentials-play-services-auth:${safeExtGet('credentialsPlayServicesAuthVersion', '1.2.2')}"
  implementation "com.google.android.libraries.identity.googleid:googleid:${safeExtGet('googleIdVersion', '1.1.0')}"
}

// Fix Gradle task dependency issues for React Native autolinking
afterEvaluate {
  // Fix resource packaging dependencies
  tasks.named("packageDebugResources") {
    dependsOn("generateDebugResValues")
  }
  tasks.named("packageReleaseResources") {
    dependsOn("generateReleaseResValues")
  }
  
  // Fix codegen task dependencies for React Native New Architecture
  tasks.matching { task ->
    task.name.contains("generateCodegenArtifactsFromSchema")
  }.configureEach { task ->
    def schemaTask = tasks.findByName("generateCodegenSchemaFromJavaScript")
    if (schemaTask != null) {
      task.dependsOn(schemaTask)
    }
  }
}
